metavar termvar, x, y , z, f, r, ys ::=
metavar typevar, X, Y , Z ::=
indexvar index, i , j, k ::=

grammar
  t, c, s :: 't_' ::=                    
    | x                   ::   :: var          
    | triv                ::   :: unit         
    {{tex \mathsf{triv} }}
    | squash U            :: M :: inj          
    {{tex \mathsf{squash}_{[[U]]} }}
    | split U             :: M :: surj         
    {{tex \mathsf{split}_{[[U]]} }}    
    | box                 ::   :: gen          
    {{tex \mathsf{box} }}
    | box A               :: M :: genA
    {{tex \mathsf{box}_{[[A]]} }}        
    | unbox A             :: M :: specA         
    {{tex \mathsf{unbox}_{[[A]]} }}
    | unbox               ::   :: spec
    {{tex \mathsf{unbox} }}
    | error A             ::    :: blame
    {{tex \mathsf{error}_{[[A]]} }}
    | error               ::    :: error
    {{tex \mathsf{error} }}    
    | Lam X <: A . t      ::    :: TypeLam
    {{tex \Lambda ([[X]] <\hspace{-2px}\colon [[A]]) .[[t]] }}
    | [ A ] t             ::   :: TypeApp
    {{tex [ [[A]] ] [[t]] }}    
    | \ x : A . t         ::   :: abs          
    {{tex \lambda ([[x]] : [[A]]) . [[t]] }}
    | t1 t2               ::   :: app          
    | ( t1 , t2 )         ::   :: pair         
    | fst t               ::   :: proj1        
    | snd t               ::   :: proj2        
    | succ t              ::   :: successor    
    | 0                   ::   :: zero         
    | case t : A of t3 -> t1 , t4 -> t2 :: :: natElim
    {{tex \mathsf{case}\,[[t]] \colon [[A]]\,\mathsf{of}\,[[t3]] \to [[t1]], [[t4]] \to [[t2]] }}
    | []                  ::   :: emptylist
    | t : : t'            ::   :: cons
    {{tex [[t]] :: [[t']] }}
    | ( t )               :: S :: parens
    | [ t1 / x ] t2       :: M :: subst
    | H( t )              :: M :: hparens
    {{tex [[t]] }}
    | squash              ::    :: injU   
    {{tex \mathsf{squash} }}
    | split               ::   :: surjU
    {{tex \mathsf{split} }}    
    | [ A / X ] t         :: M :: TypeSub
    | n                   :: M :: num
    | fix t               :: M :: fix
    {{tex \mathsf{fix}\,[[t]] }}
    | lbox C               :: M  :: lgen
    {{tex \widehat{\mathsf{box} }_{[[C]]} }}    
    | lunbox C             :: M  :: lspec
    {{tex \widehat{\mathsf{unbox} }_{[[C]]} }}
    | t1 ; t2             :: M :: Comp
    | t1 -> t2            :: M :: Arrow
    | t1 XX t2            :: M :: Prod
    {{tex [[t1]] \times [[t2]] }}
    | lsplit S            :: M  :: lsplit
    {{tex \widehat{\mathsf{split} }_{[[S]]} }}    
    | lsquash S           :: M  :: lsquash
    {{tex \widehat{\mathsf{squash} }_{[[S]]} }}
    | Box C               :: M  :: lGen
    {{tex \mathsf{Box}_{[[C]]} }}    
    | Unbox C             :: M  :: lSpec
    {{tex \mathsf{Unbox}_{[[C]]} }}
    | [| t |]             :: M :: Interp
    {{tex \interp{[[t]]} }}
    | EC [ t ]            :: M :: Plug
    | caster( A , B )     :: M :: CMor
    {{tex \mathsf{caster}([[A]],[[B]]) }}
    | | t |               :: M :: UtoG

  n, m :: 'n_' ::=
    | 0 :: :: zero
    | succ n :: :: succ

  v :: 'v_' ::=                    
    | triv                ::   :: unit         
    {{tex \mathsf{triv} }}
    | []                ::   :: Empty
    | unbox A           ::   :: specA         
    {{tex \mathsf{unbox}_{[[A]]} }}
    | Lam X <: A . t      ::    :: TypeLam
    {{tex \Lambda ([[X]] <\hspace{-2px}\colon [[A]]) .[[t]] }}
    | \ x : A . t         ::   :: abs          
    {{tex \lambda ([[x]] : [[A]]) . [[t]] }}
    | n                   ::   :: num
    | case t : A of t3 -> t1 , t4 -> t2 :: :: natElim
    {{tex \mathsf{case}\,[[t]] \colon [[A]]\,\mathsf{of}\,[[t3]] \to [[t1]], [[t4]] \to [[t2]] }}

  EC {{tex \mathcal{E} }} :: 'EC_' ::=
    | HL         :: :: Hole
    {{tex \Box }}
    | v EC       :: :: App
    | succ EC    :: :: Succ
    | fst EC     :: :: Fst
    | snd EC     :: :: Snd
    | ( EC , t ) :: :: Pair1
    | ( t , EC ) :: :: Pair2
    | case EC : A of t3 -> t1 , t4 -> t2 :: :: Case
    {{tex \mathsf{case}\,[[EC]] \colon [[A]]\,\mathsf{of}\,[[t3]] \to [[t1]], [[t4]] \to [[t2]] }}

  % U ::= ? -> ? | ? x ? | List ?
  % T is atomic
  A, B, C, D, E, S, U, K, T :: 'A_' ::=   
    | X                   ::   :: Var
    | Top                 ::   :: Top
    {{tex \top }}
    | List A              ::   :: List
    {{tex \mathsf{List}\,[[A]] }}
    | Forall ( X <: A ) . B   ::   :: Forall
    {{tex \forall ([[X]] <\hspace{-2px}\colon [[A]]). [[B]]}}
    | SL                  ::   :: Simple
    {{tex \mathbb{S} }}
    | Unit                ::   :: term    
    {{tex \mathsf{Unit} }}
    | Nat                 ::   :: nat     
    {{tex \mathsf{Nat} }}    
    | ?                   ::   :: unitype 
    | A1 -> A2            ::   :: arrow   
    | A1 x A2             ::   :: pair    
    {{tex [[A1]] \times [[A2]] }}     
    | ( A )               :: S :: paren
    | H( A )              :: M :: hparen
    {{tex [[A]] }}
    | [ A / X ] B         :: M :: TypeSub
    | A1 --> A2           :: M :: Morph
    {{tex [[A1]] \mto [[A2]] }}
    | [| G |]             :: M :: InterpCtx
    {{tex \interp{[[G]]} }}
    | [| A |]             :: M :: InterpType
    {{tex \interp{[[A]]} }}
    | skeleton A          :: M :: Skeleton
    {{tex \mathsf{skeleton}\,[[A]] }}
    
  G {{ tex \Gamma }} :: G_ ::=            
    | .                   ::   :: emptyCtx
    {{tex \cdot }}
    | G , X <: A           ::   :: tvn
    {{tex [[G]],[[X]] <\hspace{-2px}\colon [[A]] }}
    | G , x : A           ::   :: vn
    | x : A               ::   :: El
    | G , G'              :: M :: append  
    | ( G )               :: M :: parens

  terminals :: 'terminals_' ::=
    |  ->                 ::   :: arrow
    {{tex \rightarrow }}
    | |-                  ::   :: turnstile
    {{tex \vdash }}
    | |~                  ::   :: ndash
    {{tex \not \vdash }}    
    | succ                ::   :: succ
    {{tex \mathsf{succ} }}
    | ?                   ::   :: unitype
    | ~                   ::   :: cons
    {{tex \sim }}
    | ~U                  ::   :: conv
    {{tex \sim_U }}    
    | elem                ::   :: elem
    {{tex \in }}
    | ~>                  ::   :: red
    {{tex \rightsquigarrow }}
    | box        :: :: box
    {{tex \mathsf{box} }}
    | unbox        :: :: unbox
    {{tex \mathsf{unbox} }}
    | fst        :: :: fst
    {{tex \mathsf{fst} }}
    | snd        :: :: snd
    {{tex \mathsf{snd} }}                

  formula :: 'formula_' ::=
    | judgement           ::   :: judgement
    | [| judgement |]     :: M :: interp
    {{tex \interp{[[judgement]]} }}
    | x nin FV( t )       ::   :: NinFV
    {{tex [[x]] \not\in \mathsf{FV}([[t]]) }}
    | x : A elem G        ::   :: xTelemG
    | X <: A elem G        ::   :: XTelemG
    {{tex [[X]] <\hspace{-2px}\colon [[A]] \in [[G]] }}
    | formula1 && formula2 ::  :: Quad
    {{tex [[formula1]] \quad [[formula2]] }}
    | ( formula )         :: S :: parens
    {{tex [[formula]] }}    
    | t : A               :: M :: TermHasType
    | A != B              :: M :: NotEqual
    {{tex [[A]] \neq [[B]] }}
    | t ~>* t'            :: M :: Rd
    {{tex [[t]] \rightsquigarrow^* [[t']] }}
    | t ^                :: M :: Div
    {{tex [[t]] \uparrow }}    

subrules
  v <:: t
  n <:: t
  
defns
  Typing :: '' ::=

defn
    G |- A <: B :: :: subtype :: 'S_'
    {{tex [[G]] \vdash [[A]] <\hspace{-2px}\colon [[B]] }} by

    ----------- :: Refl
    G |- A <: A

    ------------- :: Top
    G |- A <: Top

    X <: A elem G
    ---------------------:: Var
    G |- X <: A

    -------------- :: TopSL
    G |- Top <: SL

    -------------- :: NatSL
    G |- Nat <: SL

    --------------- :: UnitSL
    G |- Unit <: SL

    G |- A <: SL
    ----------------- :: ListSL
    G |- List A <: SL

    G |- A <: SL && G |- B <: SL
    ---------------------------- :: ArrowSL
    G |- A -> B <: SL

    G |- A <: SL && G |- B <: SL
    ---------------------------- :: ProdSL
    G |- A x B <: SL

    G |- A <: B
    --------------------- :: List
    G |- List A <: List B

    G |- A1 <: A2 && G |- B1 <: B2
    ------------------------------ :: Prod
    G |- A1 x B1 <: A2 x B2

    G |- A2 <: A1 && G |- B1 <: B2
    ------------------------------ :: Arrow
    G |- A1 -> B1 <: A2 -> B2

    G, X <: A |- B1 <: B2
    ----------------------------------------- :: Forall
    G |- Forall (X <: A).B1 <: Forall (X <: A).B2

defn
    G1 <= G2 :: :: ctxpre :: 'CtxP_'
    {{tex [[G1]] \sqsubseteq [[G2]] }}
by

 ------ :: refl
 G <= G

 (G1 <= G2 && A <= A') && G3 <= G4
 --------------------------------- :: ext
 G1,x : A,G3 <= G2,x : A',G4

defn
    A <= B :: :: typepre :: 'P_'
    {{tex [[A]] \sqsubseteq [[B]] }}
by

   G |- A <: SL
   ------------ :: U
   A <= ?

   ------ :: refl
   A <= A

   A <= C && B <= D
   -------------------- :: arrow
   (A -> B) <= (C -> D)

   A <= C && B <= D
   ------------------ :: prod
   (A x B) <= (C x D)

   A <= B
   ------------------------- :: list
   (List A) <= (List B)

   B1 <= B2
   ------------------------------------------- :: forall
  (Forall (X <: A).B1) <= (Forall (X <: A).B2)

defn
    G |- t <= t' :: :: termpre :: 'TP_'
    {{tex [[G]] \vdash [[t]] \sqsubseteq [[t']] }}
by

    x : A elem G
    -------------------- :: var
    G |- x <= x

    S1 <= S2
    ------------------------- :: split
    G |- split S1 <= split S2

    S1 <= S2
    --------------------------- :: squash
    G |- squash S1 <= squash S2

    --------------- :: box
    G |- box <= box

    ------------------- :: unbox
    G |- unbox <= unbox

    ----------- :: nat
    G |- 0 <= 0

    ----------------- :: triv
    G |- triv <= triv

    ------------- :: empty
    G |- [] <= []

    G |- t1 <= t2
    --------------------------- :: succ
    G |- (succ t1) <= (succ t2)

    (G |- t1 <= t4 && G |- t2 <= t5) && G, x : Nat |- t3 <= t6
    -------------------------------------------------------------------------------------------- :: Nate
    G |- (case t1 : Nat of 0 -> t2, (succ x) -> t3) <= (case t4 : Nat of 0 -> t5, (succ x) -> t6)

    G |- t1 <= t3 && G |- t2 <= t4
    ------------------------------ :: pair
    G |- (t1,t2) <= (t3,t4)

    G |- t1 <= t2
    ------------------------- :: fst
    G |- (fst t1) <= (fst t2)

    G |- t1 <= t2
    ------------------------- :: snd
    G |- (snd t1) <= (snd t2)

    G |- t1 <= t3 && G |- t2 <= t4
    ------------------------------ :: cons
    G |- (t1::t2) <= (t3::t4)

    ((G |- t1 <= t4 && G |- t2 <= t5) && G, x : A2, y : List A2 |- t3 <= t6) && A1 <= A2
    ------------------------------------------------------------------------------------------------- :: Liste
    G |- (case t1 : List A1 of [] -> t2, (x::y) -> t3) <= (case t4 : List A2 of 0 -> t5, (x::y) -> t6)

    G, x : A2 |- t1 <= t2 && A1 <= A2
    --------------------------------- :: Fun
    G |- (\x:A1.t) <= (\x:A2.t2)

    G |- t1 <= t3 && G |- t2 <= t4
    ------------------------------ :: app
    G |- (t1 t2) <= (t3 t4)
    
    G |- t : ?
    --------------------- :: unboxing
    G |- (unbox A t) <= t

    G |- t : A
    ------------------- :: boxing
    G |- t <= (box A t)

    G |- t : ?
    --------------------- :: spliting
    G |- (split S t) <= t

    G |- t : S
    --------------------- :: squashing
    G |- t <= (squash S t)

    G, X <: A |- t1 <= t2
    --------------------------------------- :: tfun
    G |- (Lam X <: A.t1) <= (Lam X <: A.t2)

    G |- t1 <= t2 && A <= B
    ----------------------- :: tapp
    G |- [A]t1 <= [B]t2

    G |- t : B && A <= B    
    -------------------- :: error
    G |- error A <= t

defn
    G |- t : A :: :: type :: 'T_'
    {{tex [[G]] \vdash_{\mathsf{CG} } [[t]] : [[A]] }} by
    
    x : A elem G
    -------------------- :: varP
    G |- x : A

    x : A elem G
    -------------------- :: var
    G |- x : A

    ------------------- :: Box
    G |- box A : A -> ?

    --------------------- :: Unbox
    G |- unbox A : ? -> A

    ------------------------------------ :: BoxP
    G |- box : Forall (X <: SL).(X -> ?)

    -------------------------------------- :: UnboxP
    G |- unbox : Forall (X <: SL).(? -> X)

    ---------------------- :: squash
    G |- squash S : S -> ?

    --------------------- :: split
    G |- split S : ? -> S

    ---------------- :: unitP
    G |- triv : Unit

    ------------- :: zeroP
    G |- 0 : Nat

    G |- t : Nat
    ----------------- :: succ
    G |- succ t : Nat    

    G |- t : Nat
    G |- t1 : A  && G, x : Nat |- t2 : A
    ------------------------------------------------ :: ncase
    G |- case t : Nat of 0 -> t1, (succ x) -> t2 : A

    -------------------------------- :: empty
    G |- [] : Forall (X <: ?).List X

    G |- t1 : A && G |- t2 : List A
    ------------------------------- :: cons
    G |- t1 :: t2 : List A

    G |- t : List A
    G |- t1 : B && G, x : A, y : List A |- t2 : B
    --------------------------------------------------- :: lcase
    G |- case t : List A of [] -> t1, (x :: y) -> t2 : B

    G |- t1 : A1 && G |- t2 : A2
    ---------------------------- :: pair
    G |- (t1,t2) : A1 x A2

    G |- t : A1 x A2
    ---------------- :: fst
    G |- fst t : A1

    G |- t : A1 x A2
    ---------------- :: snd
    G |- snd t : A2

    G, x : A |- t : B
    -------------------- :: lam
    G |- \x:A.t : A -> B

    G |- t1 : A -> B && G |- t2 : A
    ---------------------------------- :: app
    G |- t1 t2 : B

    G, X <: A |- t : B
    ------------------------------------ :: Lam
    G |- Lam X <: A. t : Forall (X <: A).B

    G |- t : Forall (X<:B).C && G |- A <: B
    --------------------------------------- :: typeApp
    G |- [A]t : [A/X]C

    G |- t : A && G |- A <: B
    ------------------------- :: Sub
    G |- t : B

   ---------------- :: error
   G |- error A : A

defns
  Reduction :: '' ::=

defn
    t1 ~> t2 :: :: red :: rd_    {{com call by name }}
by

    ---------------------------- :: retracT
    unbox A (box A t) ~> t

    A != B    
    ---------------------------- :: retracTE
    unbox A (box B t) ~> error A

    ------------------------- :: retractU
    split S (squash S t) ~> t

    x : B |- EC[x] : A
    ---------------------- :: error
    EC[error B] ~> error A

    --------------------------------------------- :: ncase0
    case 0 : Nat of 0 -> t1, (succ x) -> t2 ~> t1

    --------------------------------------------------------- :: ncaseSucc
    case (succ t) : Nat of 0 -> t1, (succ x) -> t2 ~> [t/x]t2

    -------------------------------------------------- :: lcaseEmpty
    case [] : List A of [] -> t1, (x :: y) -> t2 ~> t1

    ---------------------------------------------------------------------- :: lcaseCons
    case (t1 :: t2) : List A of [] -> t3, (x :: y) -> t4 ~> [t1/x][t2/y]t4

    ------------------------- :: beta
    (\x:A1.t2) t1 ~> [t1/x]t2
    
    ------------------ :: proj1
    fst (t1, t2) ~> t1

    ------------------ :: proj2
    snd (t1, t2) ~> t2
    
    ----------------------------- :: typeBeta
    [A](Lam X <: B . t) ~> [A/X]t

    t1 ~> t2
    -------------- :: typeApp
    [A]t1 ~> [A]t2

    t1 ~> t2
    ---------------- :: Cong
    EC[t1] ~> EC[t2]